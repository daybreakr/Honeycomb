buildscript {
    ext.versions = [
            androidPlugin: '3.1.3',

            compileSdk: 27,
            minSdk: 10,
            targetSdk: 27,

            supportLibrary: '27.1.1',
    ]

    ext.deps = [
            androidPlugin: "com.android.tools.build:gradle:${versions.androidPlugin}",
            test: [
                    junit: 'junit:junit:4.12',
                    androidJunit: 'com.android.support.test:runner:1.0.2',
                    espresso: 'com.android.support.test.espresso:espresso-core:3.0.2',
            ],
            support: [
                    appcompat: "com.android.support:appcompat-v7:${versions.supportLibrary}",
                    cardview: "com.android.support:cardview-v7:${versions.supportLibrary}",
                    recyclerview: "com.android.support:recyclerview-v7:${versions.supportLibrary}",
            ],
            butterKnife: 'com.jakewharton:butterknife:8.8.1',
            butterKnifeCompiler: 'com.jakewharton:butterknife-compiler:8.8.1',
    ]

    ext.runners = [
            androidJUnitRunner: 'android.support.test.runner.AndroidJUnitRunner',
    ]

    repositories {
        mavenLocal()
        google()
        jcenter()
    }

    dependencies {
        classpath deps.androidPlugin
    }
}

subprojects {
    group = GROUP
    version = getVersionName()
    ext.versionCode = getVersionCode()

    repositories {
        mavenLocal()
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

private static def getVersionName() {
    def cmd = 'git describe --tags'
    cmd.execute().text.trim()
}

private static def getVersionCode() {
    // Get git commit count as version code.
    def cmd = 'git rev-list HEAD --count'
    cmd.execute().text.trim().toInteger()
}
