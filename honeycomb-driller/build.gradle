//==================================================================================================
// Project(Module)
//==================================================================================================
task projectInfo {
    doLast {
        println " ===== Android Library ====="
        println " ${project.name}: "
        println "   - maven: ${project.group}:${project.name}:${project.version}"
        println " ---------------------------"
        println ""
    }
}

//==================================================================================================
// Android
//==================================================================================================
apply plugin: 'com.android.library'

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk

        testInstrumentationRunner runners.androidJUnitRunner

        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all {
        it.generateBuildConfig.enabled false
    }
}

dependencies {
    //==============================================================================================
    // Test
    //==============================================================================================
    testImplementation deps.test.junit
    androidTestImplementation deps.test.androidJunit

    //==============================================================================================
    // Common
    //==============================================================================================
    implementation project(':honeycomb-basement')
}

//==================================================================================================
// Maven publish
//==================================================================================================
apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        honeycombDriller(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version

            // source code
            artifact sourcesJar {
                classifier = 'sources'
            }

            // release aar
            android.libraryVariants.all { variant ->
                if (variant.buildType.name != 'release') {
                    return
                }
                artifact variant.packageLibrary // This is the aar library
            }

            // include dependencies into pom file
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // include all 'implementation' dependencies
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}
