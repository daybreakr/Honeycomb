//==================================================================================================
// Project(Module)
//==================================================================================================
def projectGroupId = project.group
def projectArtifactId = project.name
def projectVersion = project.version

task projectInfo {
    doLast {
        println " ===== Android Library ====="
        println " ${projectArtifactId}: "
        println "   - maven: ${projectGroupId}:${projectArtifactId}:${projectVersion}"
        println " ---------------------------"
        println ""
    }
}

//==================================================================================================
// Android
//==================================================================================================
apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName projectVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    implementation project(':honeycomb-basement')
}

//==================================================================================================
// Maven publish
//==================================================================================================
apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId projectGroupId
            artifactId projectArtifactId
            version projectVersion

            // source code
            artifact sourcesJar {
                classifier = 'sources'
            }

            // release aar
            android.libraryVariants.all { variant ->
                if (variant.buildType.name == "debug") {
                    return
                }
                artifact variant.packageLibrary // This is the aar library
            }

            // include dependencies into pom file
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // include all 'implementation' dependencies
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}
